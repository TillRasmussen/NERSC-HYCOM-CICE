#! /bin/csh -f

### Change these to your own site and user directory! 
### You will need to create a Makefile Macro in bld/ and a run_ice script 
### in input_templates/.
#KAL - SITE set as env variable before running

### SYSTEM_USERDIR set to CICE dir
setenv SYSTEM_USERDIR `dirname $0` 
setenv SYSTEM_USERDIR `cd ${SYSTEM_USERDIR} && pwd` 

### Grid resolution
#setenv RES gx3 ; setenv GRID 100x110
#KAL - RES and GRID set as env variable before running
set NXGLOB = `echo $GRID | sed s/x.\*//`
set NYGLOB = `echo $GRID | sed s/.\*x//`

# Recommendations:
#   NTASK equals nprocs in ice_in 
#   use processor_shape = slenderX1 or slenderX2 in ice_in
#   one per processor with distribution_type='cartesian' or
#   squarish blocks with distribution_type='rake'
# If BLCKX (BLCKY) does not divide NXGLOB (NYGLOB) evenly, padding 
# will be used on the right (top) of the grid.
if ($RES == 'col') then   # for column configuration:
   if ( ! $?NTASK )   setenv NTASK     1       # total number of processors
   if ( ! $?BLCKX )   setenv BLCKX     5       # x-dimension of blocks ( not including )
   if ( ! $?BLCKY )   setenv BLCKY     5       # y-dimension of blocks (  ghost cells  )
   if ( ! $?NICELYR ) setenv NICELYR   7       # number of vertical layers in the ice
   if ( ! $?NSNWLYR ) setenv NSNWLYR   1       # number of vertical layers in the snow
   if ( ! $?NICECAT ) setenv NICECAT   5       # number of ice thickness categories
else                      # test problem: gx3
   if ( ! $?NTASK )   setenv NTASK    30      # total number of processors
   if ( ! $?BLCKX )   setenv BLCKX    38       # x-dimension of blocks ( not including )
   if ( ! $?BLCKY )   setenv BLCKY    36       # y-dimension of blocks (  ghost cells  )
   if ( ! $?NICELYR ) setenv NICELYR   7       # number of vertical layers in the ice
   if ( ! $?NSNWLYR ) setenv NSNWLYR   1       # number of vertical layers in the snow
   if ( ! $?NICECAT ) setenv NICECAT   5       # number of ice thickness categories
endif

if ( ! $?MXBLCKS ) then 
  # may need to increase MXBLCKS with rake distribution or padding
  @ a = $NXGLOB * $NYGLOB ; @ b = $BLCKX * $BLCKY * $NTASK  
  @ m = $a / $b ; setenv MXBLCKS $m; if ($MXBLCKS == 0) setenv MXBLCKS 1
endif
#setenv MXBLCKS 37 # if necessary (code will print proper value)
#setenv MXBLCKS 6 # if necessary (code will print proper value)

### Tracers               # match ice_in tracer_nml to conserve memory
if ( ! $?TRAGE )   setenv TRAGE   1  # set to 1 for ice age tracer
if ( ! $?TRFY )    setenv TRFY    1  # set to 1 for first-year ice area tracer
if ( ! $?TRLVL )   setenv TRLVL   1  # set to 1 for level and deformed ice tracers
if ( ! $?TRPND )   setenv TRPND   1  # set to 1 for melt pond tracers
if ( ! $?NTRAERO ) setenv NTRAERO 0  # number of aerosol tracers 
                                     # (up to max_aero in ice_domain_size.F90) 
                                     # CESM uses 3 aerosol tracers
if ( ! $?TRBRI )   setenv TRBRI   0  # set to 1 for brine height tracer
if ( ! $?NBGCLYR ) setenv NBGCLYR 0  # number of zbgc layers
if ( ! $?TRBGCS  ) setenv TRBGCS  0  # number of skeletal layer bgc tracers 
                                     # TRBGCS=0 or 2<=TRBGCS<=9)

### Specialty code
setenv CAM_ICE  no        # set to yes for CAM runs (single column) 
setenv SHRDIR   csm_share # location of CCSM shared code
setenv IO_TYPE  netcdf    # set to none if netcdf library is unavailable
                          # set to pio for parallel netcdf
setenv DITTO    no        # reproducible diagnostics
setenv BARRIERS no        # prevent MPI buffer overflow during gather/scatter 
setenv THRD     no        # set to yes for OpenMP threading

if ( $THRD == 'yes') setenv OMP_NUM_THREADS 2 # positive integer 

### File unit numbers
setenv NUMIN 11           # minimum file unit number
setenv NUMAX 99           # maximum file unit number

### Set SRCDIR and EXEDIR to your own paths!
setenv SRCDIR `dirname $0` 
setenv SRCDIR `cd ${SRCDIR} && pwd` 
setenv EXEDIR $SYSTEM_USERDIR/rundir
setenv CBLD   $SRCDIR/bld
setenv OBJDIR $EXEDIR/compile           ; if !(-d $OBJDIR) mkdir -p $OBJDIR

setenv ARCH `uname -s`
if ( $ARCH == 'UNICOS/mp') setenv ARCH UNICOS
if ( $ARCH == 'UNICOS') then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else if ( $ARCH == 'Darwin' ) then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else
   cp -f $CBLD/Makefile.std $CBLD/Makefile
endif
setenv ARCH $ARCH.$SITE
echo "comp_ice.esmf: Environment variable ARCH=$ARCH"

cd $OBJDIR

setenv COMMDIR mpi
#if ($NTASK == 1) then
#   setenv COMMDIR serial
#else
#   setenv COMMDIR mpi 
#endif
echo "comp_ice.esmf: Environment variable COMMDIR=$COMMDIR"

setenv DRVDIR esmf
echo "comp_ice.esmf: Environment variable DRVDIR=$DRVDIR"


if ($IO_TYPE == 'netcdf') then
  setenv IODIR io_netcdf
else if ($IO_TYPE == 'pio') then
  setenv IODIR io_pio
else
  setenv IODIR io_binary
endif

### List of source code directories (in order of importance).
cat >! Filepath << EOF
$SRCDIR/drivers/$DRVDIR
$SRCDIR/source
$SRCDIR/$COMMDIR
$SRCDIR/$IODIR
$SRCDIR/$SHRDIR
EOF

# MACROID set through environment variable
echo "comp_ice.esmf: Environment variable MACROID=$MACROID"
setenv MACROFILE $CBLD/Macros.$MACROID
echo "comp_ice.esmf: CICE macro file: $MACROFILE"
if ( ! -f $MACROFILE ) then
   echo "Error: Could not find CICE macro file   $MACROFILE"
   exit 2
endif


icc -o makdep $CBLD/makdep.c                         || exit 2

setenv MAKECMD make
#MAKECMD=gmake
$MAKECMD VPFILE=Filepath EXEC=$EXEDIR/cice \
           NXGLOB=$NXGLOB NYGLOB=$NYGLOB \
           BLCKX=$BLCKX BLCKY=$BLCKY MXBLCKS=$MXBLCKS \
      -f  $CBLD/Makefile MACFILE=$MACROFILE || exit 2
setenv result $status
cd ..
echo NTASK = $NTASK
echo "global N, block_size" 
echo "x    $NXGLOB,    $BLCKX"
echo "y    $NYGLOB,    $BLCKY"
echo max_blocks = $MXBLCKS
echo $TRAGE   = TRAGE,   iage tracer
echo $TRFY    = TRFY,    first-year ice tracer
echo $TRLVL   = TRLVL,   level-ice tracers
echo $TRPND   = TRPND,   melt pond tracers
echo $NTRAERO = NTRAERO, number of aerosol tracers
echo $TRBRI   = TRBRI,   brine height tracer
echo $NBGCLYR = NBGCLYR, number of bio grid layers
echo $TRBGCS  = TRBGCS,  number of BGC tracers

echo $result

